	SUBROUTINE ATMOS (ZFT,TM,SIGMA,RHO,THETA,DELTA,CA,AMU,K)
	IMPLICIT NONE

C	THIS IS A SUBROUTINE TO COMPUTE CERTAIN ELEMENTS OF THE 1962
C	U.S. STANDARD ATMOSPHERE UP TO 90 KILOMETERS.
C	CALLING SEQUENCE
C
C	CALL ATMOS (ZFT,TM,SIGMA,RHO,THETA,DELTA,CA,AMU,K)
C	ZFT   = GEOMETRIC ALTITUDE (FEET)
C	TM    = MOLECULAR SCALE TEMPERATURE (DEGREES RANKINE)
C	SIGMA = RATIO OF DENSITY TO THAT AT SEA LEVEL
C	RHO   = DENSITY (LB-SEC**2-FT**(-4) OR SLUG-FT**(-3))
C	THETA = RATIO OF TEMPERATURE TO THAT AT SEA LEVEL
C	DELTA = RATIO OF PRESSURE TO THAT AT SEA LEVEL
C	CA    = SPEED OF SOUND (FT/SEC)
C	AMU   = VISCOSITY COEFFICIENT (LB-SEC/FT**2)
C
C	K = 1   NORMAL
C	  = 2   ALTITUDE LESS THAN -5000 METERS OR GREATER THAN 90 KM
C	  = 3   FLOATING POINT OVERFLOW
C
C	ALL DATA AND FUNDAMENTAL CONSTANTS ARE IN THE METRIC SYSTEM AS
C	THESE QUANTITIES ARE DEFINED AS EXACT IN THIS SYSTEM.
C
C	THE RADIUS OF THE EARTH (REFT59) IS THE VALUE ASSOCIATED WITH THE
C	1959 ARDC ATMOSPHERE SO THAT PROGRAMS CURRENTLY USING THE LIBRARY
C	ROUTINE WILL NOT REQUIRE ALTERATION TO USE THIS ROUTINE

	REAL	ALM, ALPHA, AMU, AMUZ, AMZ, CA, CAZ, DELH, DELTA,
     1	DELTAB, FTTOKM, GZ, GZENG, H, HB, HFT, REFT59, RHO, RHOZ,
     2	RSTAR, S, SIGMA, THETA, TM, TMB, TMK, TMZ, Z, ZFT

	Integer	I, J, K, M

	COMMON	/UNITS/	SI
	LOGICAL	SI
	DIMENSION HB(10),  TMB(10),  DELTAB(10),  ALM(10)

	DATA (HB(I),TMB(I),DELTAB(I),ALM(I), I=1,10)/
     1	-5.0,	320.65,	1.75363E-00,	-6.5,
     2	00.0,	288.15,	1.00000E-00,	-6.5,
     3	11.0,	216.65,	2.23361E-01,	00.0,
     4	20.0,   216.65, 5.40328E-02,	01.0,
     5	32.0,   228.65, 8.56663E-03,	02.8,
     6	47.0,   270.65, 1.09655E-03,	00.0,
     7	52.0,   270.65,	5.82289E-04,	-2.0,
     8	61.0,   252.65, 1.79718E-06,	-4.0,
     9	79.0,   180.65, 1.02410E-05,	00.0,
     $	88.743, 180.65, 1.62230E-06,	00.0/

	DATA
     1	REFT59	/2.0855531E07/, GZ 	/9.80665/,
     2	AMZ	/28.9664     /,	RSTAR	/8.31432/,
     3	FTTOKM	/3.048E-06   /,	S	/110.4  /,
     4	AMUZ	/1.2026E-05  /,	CAZ	/1116.65/,
     6	RHOZ	/0.076476    /,	GZENG	/32.1741/

C	CONVERT GEOMETRIC ALTITUDE TO GEOPOTENTIAL ALTITUDE
C	IF IN SI UNITS, CHANGE ZFT TO FEET

	IF (SI) ZFT=ZFT*3.280833
	HFT=(REFT59/(REFT59+ZFT))*ZFT

C	CONVERT HFT AND ZFT TO KILOMETERS

	Z=FTTOKM*ZFT
	H=FTTOKM*HFT
	K=1
	TMZ=TMB(2)
	IF (H.LT.-5.0.OR.Z.GT.90.0) GOTO 7
	DO 1 M=1,0
	IF (H-HB(M)) 2,3,1
1	CONTINUE
	GOTO 7
2	M=M-1
3	DELH=H-HB(M)
	IF (ALM(M).EQ.0.0) GOTO 4
	TMK=TMB(M)+ALM(M)*DELH

C	GRADIENT IS NON ZERO, PAGE 10, EQUATION 1.2.10-(3)

	DELTA=DELTAB(M)*((TMB(M)/TMK)**(GZ*AMZ/(RSTAR*ALM(M))))
	GOTO 5
4	TMK=TMB(M)

C	GRADIENT IS ZERO, PAGE 10, EQUATION 1.2.10-(4)

	DELTA=DELTAB(M)*EXP(-GZ*AMZ*DELH/(RSTAR*TMB(M)))
5	THETA=TMK/TMZ
	SIGMA=DELTA/THETA
	ALPHA=SQRT(THETA**3)*((TMZ+S)/(TMK+S))

C	CONVERSION TO ENGLISH UNITS

	TM=1.8*TMK
	RHO=RHOZ*SIGMA/GZENG
	CA=CAZ*SQRT(THETA)
	AMU=AMUZ*ALPHA/GZENG
	IF (SI) GOTO 100
	GOTO 101

100	TM=TM/1.8
	RHO=RHO*515.379
	CA=CA*.3048
	AMU=AMU*47.880258
	ZFT=ZFT/3.280833

C	IF IN SI UNITS$
C       TM	DEGREES  KELVIN
C       RHO	KG/M**2
C       CA	M/SEC
C       AMU	(N-SEC)/M**2
C	ZFT	M

101	CONTINUE
	CALL OVERFL (J)
	GOTO (6,8),J
6	K=K+2
	GOTO 8
7	K=2
8	RETURN
	END




